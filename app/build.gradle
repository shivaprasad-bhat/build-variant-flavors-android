plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def loadConfig(type, fileName) {
    def properties = new Properties()
    def localProps = new Properties()
    def project = project.file("config")
    def local = rootProject.file("local.properties")
    properties.load(new FileInputStream("$project/$fileName"))
    localProps.load(new FileInputStream("$local"))

    properties.each {
        println "$it.key -> $it.value"
        if (it.value == "true" || it.value == "false") {
            type.buildConfigField "Boolean", it.key, it.value
        } else {
            type.buildConfigField "String", it.key, it.value
        }
    }

    type.buildConfigField "String", "CLIENT_ID", localProps["CLIENT_ID"]
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.svb.buildvariantdemo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    flavorDimensions "default"

    productFlavors {
        free {
            dimension "default"

        }

        paid {
            dimension "default"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            loadConfig(it, "production.props")
        }

        customDemo {
            initWith debug
            applicationIdSuffix ".custom"
            minifyEnabled true
            loadConfig(it, "develop.props")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        freeCustomDemo {
            res {
                srcDirs 'src/freeCustomDemo/res'
            }
        }
    }
}


dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}